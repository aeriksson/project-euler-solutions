;; Pentagonal numbers are generated by the formula, Pn=n(3n−1)/2. The first ten
;; pentagonal numbers are:
;;
;; 1, 5, 12, 22, 35, 51, 70, 92, 117, 145, ...
;;
;; It can be seen that P4 + P7 = 22 + 70 = 92 = P8. However, their difference,
;; 70 − 22 = 48, is not pentagonal.
;;
;; Find the pair of pentagonal numbers, Pj and Pk, for which their sum and
;; difference are pentagonal and D = |Pk − Pj| is minimised; what is the value
;; of D?

(load "util")

(defn pentagonal? [n]
  (integral? (/ (inc (Math/sqrt (inc (* 24 n)))) 6)))

(def pentagonal-numbers
  (map #(/ (* % (dec (* 3 %))) 2) (iterate inc 1)))

(defn euler-44 []
  (first
    (for [pk pentagonal-numbers
          pj (take-while (partial > pk) pentagonal-numbers)
          :let [sum (+ pk pj)
                diff (- pk pj)]
          :when (and (pentagonal? sum) (pentagonal? diff))]
      diff)))

(run (euler-44))
